
&НаСервере
Процедура СформироватьНаСервере()
	
	ДгрГанта.Очистить();
	
	ДгрГанта.Обновление = Ложь;
	
	
	ДгрГанта.АвтоОпределениеПолногоИнтервала = Ложь;
	ДгрГанта.УстановитьПолныйИнтервал(Период.ДатаНачала,Период.ДатаОкончания);
	
	Запрос = Новый Запрос;
	
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	НачисленияФактическийПериодДействия.Регистратор КАК Регистратор,
	               |	НачисленияФактическийПериодДействия.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	НачисленияФактическийПериодДействия.ФизическоеЛицо.Наименование КАК ФизическоеЛицоНаименование,
	               |	НачисленияФактическийПериодДействия.ВидРасчета КАК ВидРасчета,
	               |	НачисленияФактическийПериодДействия.ВидРасчета.Наименование КАК ВидРасчетаНаименование,
	               |	НачисленияФактическийПериодДействия.ПериодДействияНачало КАК ПериодДействияНачало,
	               |	НачисленияФактическийПериодДействия.ПериодДействияКонец КАК ПериодДействияКонец,
	               |	НачисленияФактическийПериодДействия.Результат КАК Результат
	               |ИЗ
	               |	РегистрРасчета.Начисления.ФактическийПериодДействия(ПериодДействия МЕЖДУ &Дата1 И &Дата2) КАК НачисленияФактическийПериодДействия
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	НачисленияФактическийПериодДействия.ВидРасчета
	               |ИТОГИ ПО
	               |	ФизическоеЛицо";
	
	Запрос.УстановитьПараметр("Дата1", Период.ДатаНачала );
	Запрос.УстановитьПараметр("Дата2", Период.ДатаОкончания);
	
	Результат = Запрос.Выполнить();
	
	ВыборкаФизическоеЛицо = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаФизическоеЛицо.Следующий() Цикл
		
		ТекущаяТочка = ДгрГанта.УстановитьТочку(ВыборкаФизическоеЛицо.ФизическоеЛицо);
		ТекущаяТочка.Текст = ВыборкаФизическоеЛицо.ФизическоеЛицоНаименование;
		
		ВыборкаДетальныеЗаписи = ВыборкаФизическоеЛицо.Выбрать();
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

		ТекущаяСерия = ДгрГанта.УстановитьСерию(ВыборкаДетальныеЗаписи.ВидРасчета);
		ТекущаяСерия.Текст = ВыборкаДетальныеЗаписи.ВидРасчетаНаименование;
		
		ТекущаяКоллекцияИнтервалов = ДгрГанта.ПолучитьЗначение(ТекущаяТочка, ТекущаяСерия);
		
		ТекущийИнтервал = ТекущаяКоллекцияИнтервалов.Добавить();
		ТекущийИнтервал.Начало = ВыборкаДетальныеЗаписи.ПериодДействияНачало;
		ТекущийИнтервал.Конец = ВыборкаДетальныеЗаписи.ПериодДействияКонец;
		ТекущийИнтервал.Расшифровка = ВыборкаДетальныеЗаписи.Регистратор;
		ТекущийИнтервал.Текст = ВыборкаДетальныеЗаписи.Результат;
		
		КонецЦикла;
		
	КонецЦикла;
	
	
	ЭлементШкалыВремени = ДгрГанта.ОбластьПостроения.ШкалаВремени.Элементы[0];
	ЭлементШкалыВремени.Единица = ТипЕдиницыШкалыВремени.Месяц;
	ЭлементШкалыВремени.Кратность = 1;
	//ЭлементШкалыВремени.ФорматДня = ФорматДняШкалыВремени.ДеньМесяца;
	ЭлементШкалыВремени.Видимость = Истина;
	
	Если ДгрГанта.ОбластьПостроения.ШкалаВремени.Элементы.Количество() = 1 Тогда
	
		ЭлементШкалыВремени = ДгрГанта.ОбластьПостроения.ШкалаВремени.Элементы.Добавить();
		
		ЭлементШкалыВремени.Единица = ТипЕдиницыШкалыВремени.День;
		ЭлементШкалыВремени.Кратность = 1;
		ЭлементШкалыВремени.ФорматДня = ФорматДняШкалыВремени.ДеньМесяца;
		ЭлементШкалыВремени.Видимость = Истина;
		
	КонецЕсли;
	
	
	ДгрГанта.ПоддержкаМасштаба = ПоддержкаМасштабаДиаграммыГанта.Период;
	
	ДгрГанта.ОбластьЗаголовка.Текст = "Фактический период действия начислений";
	
	ДгрГанта.Обновление = Истина;

	

	
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		
		ДиалогСтандартногоПериода = Новый ДиалогРедактированияСтандартногоПериода();
		
		ДиалогСтандартногоПериода.Показать(Новый ОписаниеОповещения("СформироватьЗавершение", ЭтотОбъект));

		Возврат;
		
	КонецЕсли;
	
	СформироватьНаСервере();

КонецПроцедуры


&НаКлиенте
Процедура СформироватьЗавершение(ПериодИзДиалога, ДополнительныеПараметры) Экспорт 

	Если ПериодИзДиалога = Неопределено Тогда
		
		Сообщить("Выберите период заполнения!");
		Возврат;
	
	КонецЕсли;
	
	Период = ПериодИзДиалога;
	
	СформироватьНаСервере();

КонецПроцедуры // СформироватьЗавершение()

