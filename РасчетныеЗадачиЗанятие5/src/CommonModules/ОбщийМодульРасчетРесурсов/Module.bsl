#Область РасчетРесурсовРегистровРасчета

Процедура РассчитатьРесурсы_Начисления(НаборЗаписей) Экспорт 
	
	//НаборЗаписей = Движения.ОсновныеНачисления;
	Ссылка = НаборЗаписей.Отбор.Регистратор.Значение;
	
	//Получаем упорядоченные по категориям данные текущего набора записей, записанные в БД
	Менеджер = Новый МенеджерВременныхТаблиц;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Менеджер;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.НомерСтроки КАК НомерСтроки,
	|	Начисления.ВидРасчета КАК ВидРасчета,
	|	Начисления.ПериодРегистрации КАК ПериодРегистрации,
	|	Начисления.Регистратор КАК Регистратор,
	|	Начисления.ПериодДействия КАК ПериодДействия,
	|	Начисления.ПериодДействияНачало КАК ПериодДействияНачало,
	|	Начисления.ПериодДействияКонец КАК ПериодДействияКонец,
	|	Начисления.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|	Начисления.БазовыйПериодКонец КАК БазовыйПериодКонец,
	|	Начисления.Активность КАК Активность,
	|	Начисления.Сторно КАК Сторно,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.Должность КАК Должность,
	|	Начисления.Результат КАК Результат,
	|	Начисления.ОтработаноДней КАК ОтработаноДней,
	|	Начисления.Размер КАК Размер,
	|	Начисления.ГрафикРаботы КАК ГрафикРаботы,
	|	Начисления.Показатель
	|ПОМЕСТИТЬ ВТДвижения
	|ИЗ
	|	РегистрРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.Регистратор = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТДвижения.ВидРасчета
	|ПОМЕСТИТЬ ВТВидыРасчетаДокумента
	|ИЗ
	|	ВТДвижения КАК ВТДвижения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВидыРасчетаДокумента.ВидРасчета,
	|	НачисленияВедущиеВидыРасчета.ВидРасчета КАК ВедущийВидРасчета
	|ПОМЕСТИТЬ ВТВедущиеВидыРасчета
	|ИЗ
	|	ВТВидыРасчетаДокумента КАК ВТВидыРасчетаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.ВедущиеВидыРасчета КАК НачисленияВедущиеВидыРасчета
	|		ПО ВТВидыРасчетаДокумента.ВидРасчета = НачисленияВедущиеВидыРасчета.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВедущиеВидыРасчета.ВидРасчета,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТВидыРасчетаДокумента.ВидРасчета) КАК Приоритет
	|ПОМЕСТИТЬ ВТПриоритетыВидовРасчета
	|ИЗ
	|	ВТВедущиеВидыРасчета КАК ВТВедущиеВидыРасчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыРасчетаДокумента КАК ВТВидыРасчетаДокумента
	|		ПО ВТВедущиеВидыРасчета.ВедущийВидРасчета = ВТВидыРасчетаДокумента.ВидРасчета
	|СГРУППИРОВАТЬ ПО
	|	ВТВедущиеВидыРасчета.ВидРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДвижения.НомерСтроки КАК НомерСтроки,
	|	ВТДвижения.ВидРасчета КАК ВидРасчета,
	|	ВТДвижения.ПериодРегистрации КАК ПериодРегистрации,
	|	ВТДвижения.Регистратор КАК Регистратор,
	|	ВТДвижения.ПериодДействия КАК ПериодДействия,
	|	ВТДвижения.ПериодДействияНачало КАК ПериодДействияНачало,
	|	ВТДвижения.ПериодДействияКонец КАК ПериодДействияКонец,
	|	ВТДвижения.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|	ВТДвижения.БазовыйПериодКонец КАК БазовыйПериодКонец,
	|	ВТДвижения.Активность КАК Активность,
	|	ВТДвижения.Сторно КАК Сторно,
	|	ВТДвижения.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТДвижения.Подразделение КАК Подразделение,
	|	ВТДвижения.Должность КАК Должность,
	|	ВТДвижения.Результат КАК Результат,
	|	ВТДвижения.ОтработаноДней КАК ОтработаноДней,
	|	ВТДвижения.Размер КАК Размер,
	|	ВТДвижения.ГрафикРаботы КАК ГрафикРаботы,
	|	ВТПриоритетыВидовРасчета.Приоритет КАК Приоритет,
	|	ВТДвижения.Показатель
	|ПОМЕСТИТЬ ВТДвиженияСПриоритетом
	|ИЗ
	|	ВТДвижения КАК ВТДвижения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПриоритетыВидовРасчета КАК ВТПриоритетыВидовРасчета
	|		ПО ВТДвижения.ВидРасчета = ВТПриоритетыВидовРасчета.ВидРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДвиженияСПриоритетом.НомерСтроки КАК НомерСтроки,
	|	ВТДвиженияСПриоритетом.Приоритет КАК Приоритет,
	|	ВТДвиженияСПриоритетом.ВидРасчета.СпособРасчета КАК СпособРасчета,
	|	ВТДвиженияСПриоритетом.ВидРасчета.Формула КАК Формула,
	|	ВТДвиженияСПриоритетом.Показатель,
	|	ВТДвиженияСПриоритетом.Показатель.Идентификатор КАК Идентификатор,
	|	ВТДвиженияСПриоритетом.Размер,
	|	ВТДвиженияСПриоритетом.ВидРасчета.ЗачетОтработанногоВремени КАК ЗачетОтработанногоВремени
	|ИЗ
	|	ВТДвиженияСПриоритетом КАК ВТДвиженияСПриоритетом
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет,
	|	НомерСтроки
	|ИТОГИ
	|ПО
	|	Приоритет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДвижения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТВидыРасчетаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТВедущиеВидыРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТПриоритетыВидовРасчета";

	Запрос.УстановитьПараметр("Регистратор", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаПриоритет = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	//Внешний цикл по категориям
	Пока ВыборкаПриоритет.Следующий() Цикл

		//Запрос для получения базы и данных графика для записей с текущей категорией 
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Менеджер;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВТДвиженияСПриоритетом.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТЗаписиДляРасчета
		|ИЗ
		|	ВТДвиженияСПриоритетом КАК ВТДвиженияСПриоритетом
		|ГДЕ
		|	ВТДвиженияСПриоритетом.Приоритет = &Приоритет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(НачисленияДанныеГрафика.ЗначениеДниФактическийПериодДействия, 0) КАК ЗначениеДниФактическийПериодДействия,
		|	ЕСТЬNULL(НачисленияДанныеГрафика.ЗначениеДниПериодДействия, 0) КАК ЗначениеДниПериодДействия
		|ПОМЕСТИТЬ ВТДанныеГрафика
		|ИЗ
		|	РегистрРасчета.Начисления.ДанныеГрафика(Регистратор = &Регистратор
		|	И ВидРасчета.ТребуетДанныеГрафика = ИСТИНА
		|	И НомерСтроки В
		|		(ВЫБРАТЬ
		|			ВТЗаписиДляРасчета.НомерСтроки
		|		ИЗ
		|			ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета)) КАК НачисленияДанныеГрафика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачисленияБазаНачисления.НомерСтроки КАК НомерСтроки,
		|	НачисленияБазаНачисления.РезультатБаза КАК РезультатБаза,
		|	НачисленияБазаНачисления.ОтработаноДнейБаза КАК ОтработаноДнейБаза
		|ПОМЕСТИТЬ ВТБазаВНачислениях
		|ИЗ
		|	РегистрРасчета.Начисления.БазаНачисления(&Измерения_Начисления, &Измерения_Начисления,,
		|		Регистратор = &Регистратор
		|	И ВидРасчета.ТребуетСбораБазы = ИСТИНА
		|	И НомерСтроки В
		|		(ВЫБРАТЬ
		|			ВТЗаписиДляРасчета.НомерСтроки
		|		ИЗ
		|			ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета)) КАК НачисленияБазаНачисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТЗаписиДляРасчета.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВТДанныеГрафика.ЗначениеДниФактическийПериодДействия, 0) КАК ЗначениеДниФактическийПериодДействия,
		|	ЕСТЬNULL(ВТДанныеГрафика.ЗначениеДниПериодДействия, 0) КАК ЗначениеДниПериодДействия,
		|	ЕСТЬNULL(ВТБазаВОсновныхНачислениях.РезультатБаза, 0) КАК РезультатБаза,
		|	ЕСТЬNULL(ВТБазаВОсновныхНачислениях.ОтработаноДнейБаза, 0) КАК ОтработаноДнейБаза
		|ИЗ
		|	ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеГрафика КАК ВТДанныеГрафика
		|		ПО (ВТДанныеГрафика.НомерСтроки = ВТЗаписиДляРасчета.НомерСтроки)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазаВНачислениях КАК ВТБазаВОсновныхНачислениях
		|		ПО ВТЗаписиДляРасчета.НомерСтроки = ВТБазаВОсновныхНачислениях.НомерСтроки
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТЗаписиДляРасчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТДанныеГрафика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТБазаВНачислениях";
		Измерения_Начисления = Новый Массив;
		Измерения_Начисления.Добавить("ФизическоеЛицо");
		Измерения_Начисления.Добавить("Подразделение");
		Измерения_Начисления.Добавить("Должность");

		Запрос.УстановитьПараметр("Приоритет", ВыборкаПриоритет.Приоритет);
		Запрос.УстановитьПараметр("Регистратор", Ссылка);
		Запрос.УстановитьПараметр("Измерения_Начисления", Измерения_Начисления);

		РезультатЗапроса = Запрос.Выполнить();
		
		//Выборка с данными для записей текущей категории
		Выборка_ДанныеГрафика_База = РезультатЗапроса.Выбрать();
		
		
		//Вложенный цикл по записям текущей категории
		ВыборкаДетальныеЗаписи = ВыборкаПриоритет.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

			Запись = НаборЗаписей.Получить(ВыборкаДетальныеЗаписи.НомерСтроки - 1);

			Выборка_ДанныеГрафика_База.НайтиСледующий(ВыборкаДетальныеЗаписи.НомерСтроки, "НомерСтроки");

			РасчетРесурсов(ВыборкаДетальныеЗаписи, Выборка_ДанныеГрафика_База, Запись);
			
			//Проверка - надо ли записать отработанные дни
			Если ВыборкаДетальныеЗаписи.ЗачетОтработанногоВремени Тогда

				Запись.ОтработаноДней = Выборка_ДанныеГрафика_База.ЗначениеДниФактическийПериодДействия;

			КонецЕсли;

		КонецЦикла;

		НаборЗаписей.Записать( , Истина);

	КонецЦикла;
КонецПроцедуры

Процедура РассчитатьРесурсы_Удержания(НаборЗаписей) Экспорт

	//НаборЗаписей = Движения.Удержания;
	Ссылка = НаборЗаписей.Отбор.Регистратор.Значение;
	
    //Получаем упорядоченные по категориям данные текущего набора записей, записанные в БД
	Менеджер = Новый МенеджерВременныхТаблиц;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Менеджер;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Удержания.НомерСтроки КАК НомерСтроки,
	|	Удержания.ВидРасчета КАК ВидРасчета,
	|	Удержания.ПериодРегистрации КАК ПериодРегистрации,
	|	Удержания.Регистратор КАК Регистратор,
	|	Удержания.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|	Удержания.БазовыйПериодКонец КАК БазовыйПериодКонец,
	|	Удержания.Активность КАК Активность,
	|	Удержания.Сторно КАК Сторно,
	|	Удержания.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Удержания.Подразделение КАК Подразделение,
	|	Удержания.Должность КАК Должность,
	|	Удержания.Результат КАК Результат,
	|	Удержания.Размер КАК Размер,
	|	Удержания.Показатель
	|ПОМЕСТИТЬ ВТДвижения
	|ИЗ
	|	РегистрРасчета.Удержания КАК Удержания
	|ГДЕ
	|	Удержания.Регистратор = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТДвижения.ВидРасчета КАК ВидРасчета
	|ПОМЕСТИТЬ ВТВидыРасчетаДокумента
	|ИЗ
	|	ВТДвижения КАК ВТДвижения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВидыРасчетаДокумента.ВидРасчета КАК ВидРасчета,
	|	УдержанияВедущиеВидыРасчета.ВидРасчета КАК ВедущийВидРасчета
	|ПОМЕСТИТЬ ВТВедущиеВидыРасчета
	|ИЗ
	|	ВТВидыРасчетаДокумента КАК ВТВидыРасчетаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Удержания.ВедущиеВидыРасчета КАК УдержанияВедущиеВидыРасчета
	|		ПО ВТВидыРасчетаДокумента.ВидРасчета = УдержанияВедущиеВидыРасчета.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВедущиеВидыРасчета.ВидРасчета КАК ВидРасчета,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТВидыРасчетаДокумента.ВидРасчета) КАК Приоритет
	|ПОМЕСТИТЬ ВТПриоритетыВидовРасчета
	|ИЗ
	|	ВТВедущиеВидыРасчета КАК ВТВедущиеВидыРасчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыРасчетаДокумента КАК ВТВидыРасчетаДокумента
	|		ПО ВТВедущиеВидыРасчета.ВедущийВидРасчета = ВТВидыРасчетаДокумента.ВидРасчета
	|СГРУППИРОВАТЬ ПО
	|	ВТВедущиеВидыРасчета.ВидРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДвижения.НомерСтроки КАК НомерСтроки,
	|	ВТДвижения.ВидРасчета КАК ВидРасчета,
	|	ВТДвижения.ПериодРегистрации КАК ПериодРегистрации,
	|	ВТДвижения.Регистратор КАК Регистратор,
	|	ВТДвижения.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|	ВТДвижения.БазовыйПериодКонец КАК БазовыйПериодКонец,
	|	ВТДвижения.Активность КАК Активность,
	|	ВТДвижения.Сторно КАК Сторно,
	|	ВТДвижения.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТДвижения.Подразделение КАК Подразделение,
	|	ВТДвижения.Должность КАК Должность,
	|	ВТДвижения.Результат КАК Результат,
	|	ВТДвижения.Размер КАК Размер,
	|	ВТПриоритетыВидовРасчета.Приоритет КАК Приоритет,
	|	ВТДвижения.Показатель
	|ПОМЕСТИТЬ ВТДвиженияСПриоритетом
	|ИЗ
	|	ВТДвижения КАК ВТДвижения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПриоритетыВидовРасчета КАК ВТПриоритетыВидовРасчета
	|		ПО ВТДвижения.ВидРасчета = ВТПриоритетыВидовРасчета.ВидРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДвиженияСПриоритетом.НомерСтроки КАК НомерСтроки,
	|	ВТДвиженияСПриоритетом.ВидРасчета КАК ВидРасчета,
	|	ВТДвиженияСПриоритетом.ВидРасчета.СпособРасчета КАК СпособРасчета,
	|	ВТДвиженияСПриоритетом.Показатель,
	|	ВТДвиженияСПриоритетом.Размер,
	|	ВТДвиженияСПриоритетом.Показатель.Идентификатор КАК Идентификатор,
	|	ВТДвиженияСПриоритетом.Приоритет КАК Приоритет
	|ИЗ
	|	ВТДвиженияСПриоритетом КАК ВТДвиженияСПриоритетом
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет,
	|	НомерСтроки
	|ИТОГИ
	|ПО
	|	Приоритет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДвижения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТВидыРасчетаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТВедущиеВидыРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТПриоритетыВидовРасчета";

	Запрос.УстановитьПараметр("Регистратор", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаПриоритет = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	//Внешний цикл по категориям
	Пока ВыборкаПриоритет.Следующий() Цикл
		
		//Запрос для получения базы и данных графика для записей с текущей категорией 
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Менеджер;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВТДвиженияСПриоритетом.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТЗаписиДляРасчета
		|ИЗ
		|	ВТДвиженияСПриоритетом КАК ВТДвиженияСПриоритетом
		|ГДЕ
		|	ВТДвиженияСПриоритетом.Приоритет = &Приоритет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УдержанияБазаНачисления.НомерСтроки КАК НомерСтроки,
		|	УдержанияБазаНачисления.РезультатБаза КАК РезультатБаза,
		|	УдержанияБазаНачисления.ОтработаноДнейБаза КАК ОтработаноДнейБаза
		|ПОМЕСТИТЬ ВТБазаВНачислениях
		|ИЗ
		|	РегистрРасчета.Удержания.БазаНачисления(&ИЗмерения_Удержания, &Измерения_Начисления,, Регистратор = &Регистратор
		|	И ВидРасчета.ТребуетСбораБазы = ИСТИНА
		|	И НомерСтроки В
		|		(ВЫБРАТЬ
		|			ВТЗаписиДляРасчета.НомерСтроки
		|		ИЗ
		|			ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета)) КАК УдержанияБазаНачисления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТЗаписиДляРасчета.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВТБазаВОсновныхНачислениях.РезультатБаза, 0) КАК РезультатБаза,
		|	ЕСТЬNULL(ВТБазаВОсновныхНачислениях.ОтработаноДнейБаза, 0) КАК ОтработаноДнейБаза,
		|	0 КАК ЗначениеДниПериодДействия,
		|	0 КАК ЗначениеДниФактическийПериодДействия
		|ИЗ
		|	ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазаВНачислениях КАК ВТБазаВОсновныхНачислениях
		|		ПО ВТЗаписиДляРасчета.НомерСтроки = ВТБазаВОсновныхНачислениях.НомерСтроки
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТБазаВНачислениях
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТЗаписиДляРасчета";
		Измерения_Начисления = Новый Массив;
		Измерения_Начисления.Добавить("ФизическоеЛицо");
		Измерения_Начисления.Добавить("Подразделение");
		Измерения_Начисления.Добавить("Должность");

		Измерения_Удержания = Новый Массив;
		Измерения_Удержания.Добавить("ФизическоеЛицо");
		Измерения_Удержания.Добавить("Подразделение");
		Измерения_Удержания.Добавить("Должность");

		Запрос.УстановитьПараметр("Измерения_Начисления", Измерения_Начисления);
		Запрос.УстановитьПараметр("Измерения_Удержания", Измерения_Удержания);
		Запрос.УстановитьПараметр("Приоритет", ВыборкаПриоритет.Приоритет);
		Запрос.УстановитьПараметр("Регистратор", Ссылка);

		РезультатЗапроса = Запрос.Выполнить();
		
		//Выборка с данными для записей текущей категории
		Выборка_ДанныеГрафика_База = РезультатЗапроса.Выбрать();
		
		
		//Вложенный цикл по записям текущей категории
		ВыборкаДетальныеЗаписи = ВыборкаПриоритет.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

			Запись = НаборЗаписей.Получить(ВыборкаДетальныеЗаписи.НомерСтроки - 1);

			Выборка_ДанныеГрафика_База.НайтиСледующий(ВыборкаДетальныеЗаписи.НомерСтроки, "НомерСтроки");

			РасчетРесурсов(ВыборкаДетальныеЗаписи, Выборка_ДанныеГрафика_База, Запись);

		КонецЦикла;

		НаборЗаписей.Записать( , Истина);

	КонецЦикла;

КонецПроцедуры

Процедура РасчетРесурсов(ВыборкаДетальныеЗаписи, Выборка_ДанныеГрафика_База, Запись)
	
	//1 
	Если ВыборкаДетальныеЗаписи.СпособРасчета = Перечисления.СпособыРасчета.ПоФормуле Тогда

		НормаДней = Выборка_ДанныеГрафика_База.ЗначениеДниПериодДействия;

		ФактДней = Выборка_ДанныеГрафика_База.ЗначениеДниФактическийПериодДействия;

		Если ФактДней > 0 Тогда

			Если НормаДней = 0 Тогда

				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Не заполнен график!";
				Сообщение.Сообщить();

				Возврат;

			КонецЕсли;
		КонецЕсли;

		РезультатБаза = Выборка_ДанныеГрафика_База.РезультатБаза;

		ОтработаноДнейБаза = Выборка_ДанныеГрафика_База.ОтработаноДнейБаза;

		Показатель = ВыборкаДетальныеЗаписи.Размер;

		Если СтрНайти(ВыборкаДетальныеЗаписи.Формула, ВыборкаДетальныеЗаписи.Идентификатор) > 0 Тогда

			Формула = СтрЗаменить(ВыборкаДетальныеЗаписи.Формула, ВыборкаДетальныеЗаписи.Идентификатор, "Показатель");

		Иначе
			Возврат;
		КонецЕсли;

		Выполнить ("Запись.Результат = " + Формула);
		
		//2
	ИначеЕсли ВыборкаДетальныеЗаписи.СпособРасчета = Перечисления.СпособыРасчета.ПоСреднемуЗаработку Тогда

		НормаДней = Выборка_ДанныеГрафика_База.ЗначениеДниПериодДействия;

		ФактДней = Выборка_ДанныеГрафика_База.ЗначениеДниФактическийПериодДействия;

		Если ФактДней > 0 Тогда

			Если НормаДней = 0 Тогда

				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Не заполнен график!";
				Сообщение.Сообщить();

				Возврат;

			КонецЕсли;
		КонецЕсли;

		РезультатБаза = Выборка_ДанныеГрафика_База.РезультатБаза;

		ОтработаноДнейБаза = Выборка_ДанныеГрафика_База.ОтработаноДнейБаза;

		Показатель = ВыборкаДетальныеЗаписи.Размер;

		Если СтрНайти(ВыборкаДетальныеЗаписи.Формула, ВыборкаДетальныеЗаписи.Идентификатор) > 0 Тогда

			Формула = СтрЗаменить(ВыборкаДетальныеЗаписи.Формула, ВыборкаДетальныеЗаписи.Идентификатор, "Показатель");

		Иначе
			Возврат;
		КонецЕсли;

		Выполнить ("Запись.Результат = " + Формула);

	ИначеЕсли ВыборкаДетальныеЗаписи.СпособРасчета = Перечисления.СпособыРасчета.НулеваяСумма Тогда

		Запись.Результат = 0;

	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область Взаиморасчеты

Процедура ЗаполнитьНаборЗаписей_ВзаиморасчетыСРаботниками(НаборЗаписей) Экспорт

	Ссылка = НаборЗаписей.Отбор.Регистратор.Значение;
	//НаборЗаписей = Движения.ВзаиморасчетыСРаботниками;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.ФизическоеЛицо,
	|	Начисления.Результат
	|ПОМЕСТИТЬ ВТНачислено
	|ИЗ
	|	РегистрРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.Регистратор = &Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Удержания.ФизическоеЛицо,
	|	-Удержания.Результат
	|ИЗ
	|	РегистрРасчета.Удержания КАК Удержания
	|ГДЕ
	|	Удержания.Регистратор = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТНачислено.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СУММА(ВТНачислено.Результат) КАК Сумма
	|ИЗ
	|	ВТНачислено КАК ВТНачислено
	|СГРУППИРОВАТЬ ПО
	|	ВТНачислено.ФизическоеЛицо";

	Запрос.УстановитьПараметр("Регистратор", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();

	Пока Выборка.Следующий() Цикл

		Движение = НаборЗаписей.Добавить();

		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Ссылка.Дата;
		Движение.ФизическоеЛицо = Выборка.ФизическоеЛицо;
		Движение.Сумма = Выборка.Сумма;

	КонецЦикла;

	НаборЗаписей.Записывать = Истина;

КонецПроцедуры

#КонецОбласти

#Область Перерасчеты

// Пере рассчитать ресурсы начисления.
// 
// Параметры:
//  НаборЗаписей Набор записей
Процедура ПереРассчитатьРесурсы_Начисления(НаборЗаписей) Экспорт 
	
	//НаборЗаписей = Движения.ОсновныеНачисления;
	Ссылка = НаборЗаписей.Отбор.Регистратор.Значение;
	
	//Получаем упорядоченные по категориям данные текущего набора записей, записанные в БД
	Менеджер = Новый МенеджерВременныхТаблиц;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Менеджер;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Перерасчет.ВидРасчета КАК ВидРасчета,
	|	Перерасчет.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТПерерасчет
	|ИЗ
	|	РегистрРасчета.Начисления.Перерасчет КАК Перерасчет
	|ГДЕ
	|	Перерасчет.ОбъектПерерасчета = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.НомерСтроки КАК НомерСтроки,
	|	Начисления.ВидРасчета КАК ВидРасчета,
	|	Начисления.ПериодРегистрации КАК ПериодРегистрации,
	|	Начисления.Регистратор КАК Регистратор,
	|	Начисления.ПериодДействия КАК ПериодДействия,
	|	Начисления.ПериодДействияНачало КАК ПериодДействияНачало,
	|	Начисления.ПериодДействияКонец КАК ПериодДействияКонец,
	|	Начисления.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|	Начисления.БазовыйПериодКонец КАК БазовыйПериодКонец,
	|	Начисления.Активность КАК Активность,
	|	Начисления.Сторно КАК Сторно,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.Должность КАК Должность,
	|	Начисления.Результат КАК Результат,
	|	Начисления.ОтработаноДней КАК ОтработаноДней,
	|	Начисления.Размер КАК Размер,
	|	Начисления.ГрафикРаботы КАК ГрафикРаботы,
	|	Начисления.Показатель
	|ПОМЕСТИТЬ ВТДвижения
	|ИЗ
	|	РегистрРасчета.Начисления КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПерерасчет КАК ВТПерерасчет
	|		ПО (ВТПерерасчет.ВидРасчета = Начисления.ВидРасчета)
	|		И (ВТПерерасчет.ФизическоеЛицо = Начисления.ФизическоеЛицо)
	|ГДЕ
	|	Начисления.Регистратор = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТДвижения.ВидРасчета
	|ПОМЕСТИТЬ ВТВидыРасчетаДокумента
	|ИЗ
	|	ВТДвижения КАК ВТДвижения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВидыРасчетаДокумента.ВидРасчета,
	|	НачисленияВедущиеВидыРасчета.ВидРасчета КАК ВедущийВидРасчета
	|ПОМЕСТИТЬ ВТВедущиеВидыРасчета
	|ИЗ
	|	ВТВидыРасчетаДокумента КАК ВТВидыРасчетаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.ВедущиеВидыРасчета КАК НачисленияВедущиеВидыРасчета
	|		ПО ВТВидыРасчетаДокумента.ВидРасчета = НачисленияВедущиеВидыРасчета.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВедущиеВидыРасчета.ВидРасчета,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТВидыРасчетаДокумента.ВидРасчета) КАК Приоритет
	|ПОМЕСТИТЬ ВТПриоритетыВидовРасчета
	|ИЗ
	|	ВТВедущиеВидыРасчета КАК ВТВедущиеВидыРасчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыРасчетаДокумента КАК ВТВидыРасчетаДокумента
	|		ПО ВТВедущиеВидыРасчета.ВедущийВидРасчета = ВТВидыРасчетаДокумента.ВидРасчета
	|СГРУППИРОВАТЬ ПО
	|	ВТВедущиеВидыРасчета.ВидРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДвижения.НомерСтроки КАК НомерСтроки,
	|	ВТДвижения.ВидРасчета КАК ВидРасчета,
	|	ВТДвижения.ПериодРегистрации КАК ПериодРегистрации,
	|	ВТДвижения.Регистратор КАК Регистратор,
	|	ВТДвижения.ПериодДействия КАК ПериодДействия,
	|	ВТДвижения.ПериодДействияНачало КАК ПериодДействияНачало,
	|	ВТДвижения.ПериодДействияКонец КАК ПериодДействияКонец,
	|	ВТДвижения.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|	ВТДвижения.БазовыйПериодКонец КАК БазовыйПериодКонец,
	|	ВТДвижения.Активность КАК Активность,
	|	ВТДвижения.Сторно КАК Сторно,
	|	ВТДвижения.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТДвижения.Подразделение КАК Подразделение,
	|	ВТДвижения.Должность КАК Должность,
	|	ВТДвижения.Результат КАК Результат,
	|	ВТДвижения.ОтработаноДней КАК ОтработаноДней,
	|	ВТДвижения.Размер КАК Размер,
	|	ВТДвижения.ГрафикРаботы КАК ГрафикРаботы,
	|	ВТПриоритетыВидовРасчета.Приоритет КАК Приоритет,
	|	ВТДвижения.Показатель
	|ПОМЕСТИТЬ ВТДвиженияСПриоритетом
	|ИЗ
	|	ВТДвижения КАК ВТДвижения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПриоритетыВидовРасчета КАК ВТПриоритетыВидовРасчета
	|		ПО ВТДвижения.ВидРасчета = ВТПриоритетыВидовРасчета.ВидРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДвиженияСПриоритетом.НомерСтроки КАК НомерСтроки,
	|	ВТДвиженияСПриоритетом.Приоритет КАК Приоритет,
	|	ВТДвиженияСПриоритетом.ВидРасчета.СпособРасчета КАК СпособРасчета,
	|	ВТДвиженияСПриоритетом.ВидРасчета.Формула КАК Формула,
	|	ВТДвиженияСПриоритетом.Показатель,
	|	ВТДвиженияСПриоритетом.Показатель.Идентификатор КАК Идентификатор,
	|	ВТДвиженияСПриоритетом.Размер,
	|	ВТДвиженияСПриоритетом.ВидРасчета.ЗачетОтработанногоВремени КАК ЗачетОтработанногоВремени
	|ИЗ
	|	ВТДвиженияСПриоритетом КАК ВТДвиженияСПриоритетом
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет,
	|	НомерСтроки
	|ИТОГИ
	|ПО
	|	Приоритет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДвижения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТВидыРасчетаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТВедущиеВидыРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТПриоритетыВидовРасчета";
	Запрос.УстановитьПараметр("Регистратор", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаПриоритет = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	//Внешний цикл по категориям
	Пока ВыборкаПриоритет.Следующий() Цикл
		
		//Запрос для получения базы и данных графика для записей с текущей категорией 
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Менеджер;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВТДвиженияСПриоритетом.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТЗаписиДляРасчета
		|ИЗ
		|	ВТДвиженияСПриоритетом КАК ВТДвиженияСПриоритетом
		|ГДЕ
		|	ВТДвиженияСПриоритетом.Приоритет = &Приоритет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(НачисленияДанныеГрафика.ЗначениеДниФактическийПериодДействия, 0) КАК ЗначениеДниФактическийПериодДействия,
		|	ЕСТЬNULL(НачисленияДанныеГрафика.ЗначениеДниПериодДействия, 0) КАК ЗначениеДниПериодДействия
		|ПОМЕСТИТЬ ВТДанныеГрафика
		|ИЗ
		|	РегистрРасчета.Начисления.ДанныеГрафика(Регистратор = &Регистратор
		|	И ВидРасчета.ТребуетДанныеГрафика = ИСТИНА
		|	И НомерСтроки В
		|		(ВЫБРАТЬ
		|			ВТЗаписиДляРасчета.НомерСтроки
		|		ИЗ
		|			ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета)) КАК НачисленияДанныеГрафика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачисленияБазаНачисления.НомерСтроки КАК НомерСтроки,
		|	НачисленияБазаНачисления.РезультатБаза КАК РезультатБаза,
		|	НачисленияБазаНачисления.ОтработаноДнейБаза КАК ОтработаноДнейБаза
		|ПОМЕСТИТЬ ВТБазаВНачислениях
		|ИЗ
		|	РегистрРасчета.Начисления.БазаНачисления(&Измерения_Начисления, &Измерения_Начисления,,
		|		Регистратор = &Регистратор
		|	И ВидРасчета.ТребуетСбораБазы = ИСТИНА
		|	И НомерСтроки В
		|		(ВЫБРАТЬ
		|			ВТЗаписиДляРасчета.НомерСтроки
		|		ИЗ
		|			ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета)) КАК НачисленияБазаНачисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТЗаписиДляРасчета.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВТДанныеГрафика.ЗначениеДниФактическийПериодДействия, 0) КАК ЗначениеДниФактическийПериодДействия,
		|	ЕСТЬNULL(ВТДанныеГрафика.ЗначениеДниПериодДействия, 0) КАК ЗначениеДниПериодДействия,
		|	ЕСТЬNULL(ВТБазаВОсновныхНачислениях.РезультатБаза, 0) КАК РезультатБаза,
		|	ЕСТЬNULL(ВТБазаВОсновныхНачислениях.ОтработаноДнейБаза, 0) КАК ОтработаноДнейБаза
		|ИЗ
		|	ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеГрафика КАК ВТДанныеГрафика
		|		ПО (ВТДанныеГрафика.НомерСтроки = ВТЗаписиДляРасчета.НомерСтроки)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазаВНачислениях КАК ВТБазаВОсновныхНачислениях
		|		ПО ВТЗаписиДляРасчета.НомерСтроки = ВТБазаВОсновныхНачислениях.НомерСтроки
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТЗаписиДляРасчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТДанныеГрафика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТБазаВНачислениях";
		
		Измерения_Начисления = Новый Массив;
		Измерения_Начисления.Добавить("ФизическоеЛицо");
		Измерения_Начисления.Добавить("Подразделение");
		Измерения_Начисления.Добавить("Должность");

		Запрос.УстановитьПараметр("Приоритет", ВыборкаПриоритет.Приоритет);
		Запрос.УстановитьПараметр("Регистратор", Ссылка);
		Запрос.УстановитьПараметр("Измерения_Начисления", Измерения_Начисления);

		РезультатЗапроса = Запрос.Выполнить();
		
		//Выборка с данными для записей текущей категории
		Выборка_ДанныеГрафика_База = РезультатЗапроса.Выбрать();
		
		
		//Вложенный цикл по записям текущей категории
		ВыборкаДетальныеЗаписи = ВыборкаПриоритет.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

			Запись = НаборЗаписей.Получить(ВыборкаДетальныеЗаписи.НомерСтроки - 1);

			Выборка_ДанныеГрафика_База.НайтиСледующий(ВыборкаДетальныеЗаписи.НомерСтроки, "НомерСтроки");

			РасчетРесурсов(ВыборкаДетальныеЗаписи, Выборка_ДанныеГрафика_База, Запись);
			
			//Проверка - надо ли записать отработанные дни
			Если ВыборкаДетальныеЗаписи.ЗачетОтработанногоВремени Тогда

				Запись.ОтработаноДней = Выборка_ДанныеГрафика_База.ЗначениеДниФактическийПериодДействия;

			КонецЕсли;

		КонецЦикла;

		НаборЗаписей.Записать( , , Ложь, Ложь);

	КонецЦикла;

	НаборЗаписей.Записать( , , Ложь, Истина);

КонецПроцедуры

// Пере рассчитать ресурсы удержания.
// 
// Параметры:
//  НаборЗаписей Набор записей
Процедура ПереРассчитатьРесурсы_Удержания(НаборЗаписей) Экспорт

	//НаборЗаписей = Движения.Удержания;
	Ссылка = НаборЗаписей.Отбор.Регистратор.Значение;
	
    //Получаем упорядоченные по категориям данные текущего набора записей, записанные в БД
	Менеджер = Новый МенеджерВременныхТаблиц;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Менеджер;
	Запрос.Текст = "ВЫБРАТЬ
				   |	Перерасчет.ВидРасчета,
				   |	Перерасчет.ФизическоеЛицо
				   |ПОМЕСТИТЬ ВТПерерасчет
				   |ИЗ
				   |	РегистрРасчета.Удержания.Перерасчет КАК Перерасчет
				   |ГДЕ
				   |	Перерасчет.ОбъектПерерасчета = &Регистратор
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	Удержания.НомерСтроки КАК НомерСтроки,
				   |	Удержания.ВидРасчета КАК ВидРасчета,
				   |	Удержания.ПериодРегистрации КАК ПериодРегистрации,
				   |	Удержания.Регистратор КАК Регистратор,
				   |	Удержания.БазовыйПериодНачало КАК БазовыйПериодНачало,
				   |	Удержания.БазовыйПериодКонец КАК БазовыйПериодКонец,
				   |	Удержания.Активность КАК Активность,
				   |	Удержания.Сторно КАК Сторно,
				   |	Удержания.ФизическоеЛицо КАК ФизическоеЛицо,
				   |	Удержания.Подразделение КАК Подразделение,
				   |	Удержания.Должность КАК Должность,
				   |	Удержания.Результат КАК Результат,
				   |	Удержания.Размер КАК Размер,
				   |	Удержания.Показатель
				   |ПОМЕСТИТЬ ВТДвижения
				   |ИЗ
				   |	РегистрРасчета.Удержания КАК Удержания
				   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПерерасчет КАК ВТПерерасчет
				   |		ПО Удержания.ВидРасчета = ВТПерерасчет.ВидРасчета
				   |		И Удержания.ФизическоеЛицо = ВТПерерасчет.ФизическоеЛицо
				   |ГДЕ
				   |	Удержания.Регистратор = &Регистратор
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ РАЗЛИЧНЫЕ
				   |	ВТДвижения.ВидРасчета КАК ВидРасчета
				   |ПОМЕСТИТЬ ВТВидыРасчетаДокумента
				   |ИЗ
				   |	ВТДвижения КАК ВТДвижения
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВТВидыРасчетаДокумента.ВидРасчета КАК ВидРасчета,
				   |	УдержанияВедущиеВидыРасчета.ВидРасчета КАК ВедущийВидРасчета
				   |ПОМЕСТИТЬ ВТВедущиеВидыРасчета
				   |ИЗ
				   |	ВТВидыРасчетаДокумента КАК ВТВидыРасчетаДокумента
				   |		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Удержания.ВедущиеВидыРасчета КАК УдержанияВедущиеВидыРасчета
				   |		ПО ВТВидыРасчетаДокумента.ВидРасчета = УдержанияВедущиеВидыРасчета.Ссылка
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВТВедущиеВидыРасчета.ВидРасчета КАК ВидРасчета,
				   |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТВидыРасчетаДокумента.ВидРасчета) КАК Приоритет
				   |ПОМЕСТИТЬ ВТПриоритетыВидовРасчета
				   |ИЗ
				   |	ВТВедущиеВидыРасчета КАК ВТВедущиеВидыРасчета
				   |		ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыРасчетаДокумента КАК ВТВидыРасчетаДокумента
				   |		ПО ВТВедущиеВидыРасчета.ВедущийВидРасчета = ВТВидыРасчетаДокумента.ВидРасчета
				   |СГРУППИРОВАТЬ ПО
				   |	ВТВедущиеВидыРасчета.ВидРасчета
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВТДвижения.НомерСтроки КАК НомерСтроки,
				   |	ВТДвижения.ВидРасчета КАК ВидРасчета,
				   |	ВТДвижения.ПериодРегистрации КАК ПериодРегистрации,
				   |	ВТДвижения.Регистратор КАК Регистратор,
				   |	ВТДвижения.БазовыйПериодНачало КАК БазовыйПериодНачало,
				   |	ВТДвижения.БазовыйПериодКонец КАК БазовыйПериодКонец,
				   |	ВТДвижения.Активность КАК Активность,
				   |	ВТДвижения.Сторно КАК Сторно,
				   |	ВТДвижения.ФизическоеЛицо КАК ФизическоеЛицо,
				   |	ВТДвижения.Подразделение КАК Подразделение,
				   |	ВТДвижения.Должность КАК Должность,
				   |	ВТДвижения.Результат КАК Результат,
				   |	ВТДвижения.Размер КАК Размер,
				   |	ВТПриоритетыВидовРасчета.Приоритет КАК Приоритет,
				   |	ВТДвижения.Показатель
				   |ПОМЕСТИТЬ ВТДвиженияСПриоритетом
				   |ИЗ
				   |	ВТДвижения КАК ВТДвижения
				   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПриоритетыВидовРасчета КАК ВТПриоритетыВидовРасчета
				   |		ПО ВТДвижения.ВидРасчета = ВТПриоритетыВидовРасчета.ВидРасчета
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ВТДвиженияСПриоритетом.НомерСтроки КАК НомерСтроки,
				   |	ВТДвиженияСПриоритетом.ВидРасчета КАК ВидРасчета,
				   |	ВТДвиженияСПриоритетом.ВидРасчета.СпособРасчета КАК СпособРасчета,
				   |	ВТДвиженияСПриоритетом.Показатель,
				   |	ВТДвиженияСПриоритетом.Размер,
				   |	ВТДвиженияСПриоритетом.Показатель.Идентификатор КАК Идентификатор,
				   |	ВТДвиженияСПриоритетом.Приоритет КАК Приоритет
				   |ИЗ
				   |	ВТДвиженияСПриоритетом КАК ВТДвиженияСПриоритетом
				   |
				   |УПОРЯДОЧИТЬ ПО
				   |	Приоритет,
				   |	НомерСтроки
				   |ИТОГИ
				   |ПО
				   |	Приоритет
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |УНИЧТОЖИТЬ ВТДвижения
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |УНИЧТОЖИТЬ ВТВидыРасчетаДокумента
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |УНИЧТОЖИТЬ ВТВедущиеВидыРасчета
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |УНИЧТОЖИТЬ ВТПриоритетыВидовРасчета";

	Запрос.УстановитьПараметр("Регистратор", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаПриоритет = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	//Внешний цикл по категориям
	Пока ВыборкаПриоритет.Следующий() Цикл
		
		//Запрос для получения базы и данных графика для записей с текущей категорией 
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Менеджер;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВТДвиженияСПриоритетом.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТЗаписиДляРасчета
		|ИЗ
		|	ВТДвиженияСПриоритетом КАК ВТДвиженияСПриоритетом
		|ГДЕ
		|	ВТДвиженияСПриоритетом.Приоритет = &Приоритет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УдержанияБазаНачисления.НомерСтроки КАК НомерСтроки,
		|	УдержанияБазаНачисления.РезультатБаза КАК РезультатБаза,
		|	УдержанияБазаНачисления.ОтработаноДнейБаза КАК ОтработаноДнейБаза
		|ПОМЕСТИТЬ ВТБазаВНачислениях
		|ИЗ
		|	РегистрРасчета.Удержания.БазаНачисления(&ИЗмерения_Удержания, &Измерения_Начисления,, Регистратор = &Регистратор
		|	И ВидРасчета.ТребуетСбораБазы = ИСТИНА
		|	И НомерСтроки В
		|		(ВЫБРАТЬ
		|			ВТЗаписиДляРасчета.НомерСтроки
		|		ИЗ
		|			ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета)) КАК УдержанияБазаНачисления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТЗаписиДляРасчета.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВТБазаВОсновныхНачислениях.РезультатБаза, 0) КАК РезультатБаза,
		|	ЕСТЬNULL(ВТБазаВОсновныхНачислениях.ОтработаноДнейБаза, 0) КАК ОтработаноДнейБаза,
		|	0 КАК ЗначениеДниПериодДействия,
		|	0 КАК ЗначениеДниФактическийПериодДействия
		|ИЗ
		|	ВТЗаписиДляРасчета КАК ВТЗаписиДляРасчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазаВНачислениях КАК ВТБазаВОсновныхНачислениях
		|		ПО ВТЗаписиДляРасчета.НомерСтроки = ВТБазаВОсновныхНачислениях.НомерСтроки
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТБазаВНачислениях
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТЗаписиДляРасчета";
		Измерения_Начисления = Новый Массив;
		Измерения_Начисления.Добавить("ФизическоеЛицо");
		Измерения_Начисления.Добавить("Подразделение");
		Измерения_Начисления.Добавить("Должность");

		Измерения_Удержания = Новый Массив;
		Измерения_Удержания.Добавить("ФизическоеЛицо");
		Измерения_Удержания.Добавить("Подразделение");
		Измерения_Удержания.Добавить("Должность");

		Запрос.УстановитьПараметр("Измерения_Начисления", Измерения_Начисления);
		Запрос.УстановитьПараметр("Измерения_Удержания", Измерения_Удержания);
		Запрос.УстановитьПараметр("Приоритет", ВыборкаПриоритет.Приоритет);
		Запрос.УстановитьПараметр("Регистратор", Ссылка);

		РезультатЗапроса = Запрос.Выполнить();
		
		//Выборка с данными для записей текущей категории
		Выборка_ДанныеГрафика_База = РезультатЗапроса.Выбрать();
		
		
		//Вложенный цикл по записям текущей категории
		ВыборкаДетальныеЗаписи = ВыборкаПриоритет.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

			Запись = НаборЗаписей.Получить(ВыборкаДетальныеЗаписи.НомерСтроки - 1);

			Выборка_ДанныеГрафика_База.НайтиСледующий(ВыборкаДетальныеЗаписи.НомерСтроки, "НомерСтроки");

			РасчетРесурсов(ВыборкаДетальныеЗаписи, Выборка_ДанныеГрафика_База, Запись);

		КонецЦикла;

		НаборЗаписей.Записать( , , Ложь, Ложь);

	КонецЦикла;

	НаборЗаписей.Записать( , , Ложь, Истина);

КонецПроцедуры

#КонецОбласти