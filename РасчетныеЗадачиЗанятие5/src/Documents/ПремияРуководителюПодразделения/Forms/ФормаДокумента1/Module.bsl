
&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	
	ЗаполнитьРеквизитыДокументаНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыДокументаНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РуководителиПодразделенийСрезПоследних.Подразделение,
	|	РуководителиПодразделенийСрезПоследних.Руководитель,
	|	РуководителиПодразделенийСрезПоследних.РазмерПремии
	|ПОМЕСТИТЬ ВТРуководитель
	|ИЗ
	|	РегистрСведений.РуководителиПодразделений.СрезПоследних(&НачалоМесяца, Подразделение = &Подразделение) КАК
	|		РуководителиПодразделенийСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиСрезПоследних.Подразделение,
	|	СотрудникиСрезПоследних.ФизическоеЛицо,
	|	СотрудникиСрезПоследних.Должность,
	|	ВТРуководитель.РазмерПремии,
	|	СотрудникиСрезПоследних.ГрафикРаботы
	|ИЗ
	|	РегистрСведений.Сотрудники.СрезПоследних(&НачалоМесяца, (ФизическоеЛицо, Подразделение) В
	|		(ВЫБРАТЬ
	|			ВТРуководитель.Руководитель,
	|			ВТРуководитель.Подразделение
	|		ИЗ
	|			ВТРуководитель КАК ВТРуководитель)) КАК СотрудникиСрезПоследних,
	|	ВТРуководитель КАК ВТРуководитель";
	
	Запрос.УстановитьПараметр("НачалоМесяца", Объект.ПериодРегистрации);
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл

		Объект.ФизическоеЛицо = Выборка.ФизическоеЛицо;
		Объект.Должность = Выборка.Должность;
		Объект.ГрафикРаботы = Выборка.ГрафикРаботы;
		Объект.РазмерПремии = Выборка.РазмерПремии;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииПриИзменении(Элемент)
	
	Объект.ПериодРегистрации = НачалоМесяца(Объект.ПериодРегистрации);
	
	ЗаполнитьРеквизитыДокументаНаСервере();
	
КонецПроцедуры


&НаКлиенте
Процедура РассчитатьРесурсы(Команда)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) = Ложь Тогда
		
		ПоказатьПредупреждение(,"Для расчета документ надо записать!",15,"Это новый документ!");
		
		Возврат;
		
	КонецЕсли;
	
	
	Если Объект.Проведен = Истина Тогда
	
		ПоказатьВопрос(
		Новый ОписаниеОповещения("РассчитатьРесурсыЗавершение", ЭтотОбъект)
		,"Рассчитывать можно только НЕПРОВЕДЕННЫЙ документ. Отменить проведение?",
		РежимДиалогаВопрос.ДаНет,
		30, КодВозвратаДиалога.Да,"Внимание! Документ проведен.", КодВозвратаДиалога.Да);
		
		Возврат;
	
	КонецЕсли;
	
	РассчитатьРесурсыНаСервере();
	
	ТекущийЭлемент = Элементы.СтраницаНачисления;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьРесурсыЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт 

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.ОтменаПроведения));
		
		РассчитатьРесурсыНаСервере();
		
		ТекущийЭлемент = Элементы.СтраницаНачисления;
	
	КонецЕсли;
	
	

КонецПроцедуры // РассчитатьРесурсыЗавершение()

&НаСервере
Процедура РассчитатьРесурсыНаСервере()
	
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	
	ДокументОбъект.РассчитатьРесурсы(); //Вызов экспортной процедуры объекта
	
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
	
КонецПроцедуры


